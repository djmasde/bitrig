#!/usr/bin/awk -f
#
# Generates a login.conf file from login.conf.in and an optional
# file containing overrides.
#
# Usage: mklogin.conf [overrides_file] < login.conf.in > login.conf.out
#
# $OpenBSD: mklogin.conf,v 1.8 2014/03/19 04:04:25 tedu Exp $

BEGIN {
	# Default substitutions
	values["DEF_DATASIZE_MAX"]="2048M"
	values["DEF_DATASIZE_CUR"]="1024M"
	values["DEF_MAXPROC_MAX"]="1024"
	values["DEF_MAXPROC_CUR"]="512"
	values["DEF_OPENFILES_CUR"]="2048"
	values["STAFF_DATASIZE_MAX"]="infinity"
	values["STAFF_DATASIZE_CUR"]="1024M"
	values["STAFF_MAXPROC_MAX"]="1024"
	values["STAFF_MAXPROC_CUR"]="512"
	values["STAFF_OPENFILES_CUR"]="1024"
	values["DEF_BLOWFISH_RNDS"]="8"
	values["ROOT_BLOWFISH_RNDS"]="9"

	# Optional overrides
	if (ARGC > 1) {
		while (getline override < ARGV[1]) {
			split(override, ov)
			values[ov[1] ""]=ov[2] ""
		}
		ARGC--
	}
}

# Read in login.conf template, do substitutions and output the result
{
	for (key in values)
		gsub("@" key "@", values[key], $0)
	print $0
}
